include: resource://Neos.Fusion/Private/Fusion/Root.fusion
include: resource://Neos.Fusion.Form/Private/Fusion/Root.fusion

Flowpack.Neos.FrontendLogin.AuthenticationController.index = Neos.Fusion:Component {
    account = ${account}
    flashMessages = ${flashMessages}

    settings = ${Configuration.setting('Flowpack.Neos.FrontendLogin')}

    renderer = afx`
        <div class="panel panel-default">
            <div class="panel-body">
                <p @if.generalAccess={Security.hasAccess('Neos.Neos:Backend.GeneralAccess')}>
                    {I18n.id('authentication.backendmessage').package(props.settings.translation.packageKey).source(props.settings.translation.sourceName).translate()}
                </p>
                <div @if.hasUserRole={Security.hasRole('Flowpack.Neos.FrontendLogin:User')}>
                    <p>
                        {I18n.id('authentication.form.loggedIn').value('You are logged in as').package(props.settings.translation.packageKey).source(props.settings.translation.sourceName).translate()}&nbsp;
                        <strong>"{props.account.accountIdentifier}"</strong>
                    </p>
                    <Neos.Fusion.Form:Form form.target.action="logout" attributes.class="form-horizontal clearfix" attributes.role="form">
                        <Neos.Fusion.Form:Button attributes.class="btn btn-primary">
                            {I18n.id('authentication.form.logout').value('Logout').package(props.settings.translation.packageKey).source(props.settings.translation.sourceName).translate()}
                        </Neos.Fusion.Form:Button>
                    </Neos.Fusion.Form:Form>
                </div>
                <div @if.hasNotUserRole={!Security.hasAccess('Neos.Neos:Backend.GeneralAccess') && !Security.hasRole('Flowpack.Neos.FrontendLogin:User')}>
                    <ul @if.hasFlashMessages={props.flashMessages}>
                        <Neos.Fusion:Loop items={props.flashMessages} itemName="flashMessage">
                            <li @if.isError={flashMessage.severity == 'Error'} class="flashmessages-error">{flashMessage.message}</li>
                        </Neos.Fusion:Loop>
                    </ul>

                    <Neos.Fusion.Form:Form form.target.action="authenticate" attributes.class="form-horizontal clearfix" attributes.role="form">
                        <div class="form-group">
                            <label for="flowpack-neos-frontendlogin-username" class="col-sm-2 control-label">
                                {I18n.id('authentication.form.username').package(props.settings.translation.packageKey).source(props.settings.translation.sourceName).translate()}:
                            </label>
                            <div class="col-sm-10">
                                <Neos.Fusion.Form:Input
                                    field.name="__authentication[Neos][Flow][Security][Authentication][Token][UsernamePassword][username]"
                                    attributes.id="flowpack-neos-frontendlogin-username"
                                    attributes.class="form-control"
                                />
                            </div>
                        </div>
                        <div class="form-group">
                            <label for="flowpack-neos-frontendlogin-password" class="col-sm-2 control-label">
                                {I18n.id('authentication.form.password').package(props.settings.translation.packageKey).source(props.settings.translation.sourceName).translate()}:
                            </label>
                            <div class="col-sm-10">
                                <Neos.Fusion.Form:Password
                                    field.name="__authentication[Neos][Flow][Security][Authentication][Token][UsernamePassword][password]"
                                    attributes.id="flowpack-neos-frontendlogin-password"
                                    attributes.class="form-control"
                                />
                            </div>
                        </div>
                        <div class="form-group">
                            <div class="pull-right">
                                <Neos.Fusion.Form:Button attributes.class="btn btn-primary">
                                    {I18n.id('authentication.form.submit').package(props.settings.translation.packageKey).source(props.settings.translation.sourceName).translate()}
                                </Neos.Fusion.Form:Button>
                            </div>
                        </div>
                    </Neos.Fusion.Form:Form>
                </div>
            </div>
        </div>
    `
}
